module common.utils;

import arsd.characterencodings;
import std.algorithm;
import std.ascii;
import std.conv;
import std.datetime;
import std.file;
import std.path;
import std.random;
import std.range;
import std.regex;
import std.string;
import std.typecons;
import std.utf;
import vibe.core.log;
/**
 * From removes variants of "Re:"/"RE:"/"re:" in the subject
 */
auto MSGID_REGEX = ctRegex!(r"[\w@.=%+\-!#\$&'\*/\?\^`\{\}\|~]*\b", "g");
auto EMAIL_REGEX = ctRegex!(r"[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\.[a-zA-Z]{2,4}\b", "g");
auto EMAIL_REGEX2 = ctRegex!(r"<?[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\.[>a-zA-Z]{2,4}\b>?", "g");
auto SUBJECT_CLEAN_REGEX = ctRegex!(r"([\[\(] *)?(RE?) *([-:;)\]][ :;\])-]*|$)|\]+ *$", "gi");

string clearSubject(in string subject)
{
    return replaceAll!(x => "")(subject, SUBJECT_CLEAN_REGEX);
}


T[] removeDups(T)(in T[] input)
pure nothrow
{
    bool[T] dict;
    T[] result;

    foreach(T item; input)
    {
        if (item !in dict)
        {
            dict[item] = true;
            result ~= item;
        }
    }
    return result;
}


string randomString(in uint length)
{
    return iota(length).map!(_ => lowercase[uniform(0, $)]).array;
}


string randomFileName(in string directory, in string extension="")
{
    string destPath;
    string ext = extension;

    // ensure .something
    if (ext.length > 0 && !ext.startsWith("."))
        ext = "." ~ ext;

    // yep, not 100% warranteed to be unique but incredibly improbable, still FIXME
    do
    {
        destPath = format("%d_%s%s",
                          stdTimeToUnixTime(Clock.currStdTime),
                          randomString(10),
                          ext);
    } while (destPath.exists);
    return buildPath(directory, destPath);
}


bool lowStartsWith(in string input, in string startsw)
pure
{
    return lowStrip(input).startsWith(startsw);
}


string lowStrip(in string input)
pure
{
    return toLower(strip(input));
}


string generateMessageId(in string domain="")
{
    string dom = domain;
    if (dom.length == 0)
        dom = randomString(10) ~ ".com";

    immutable preDomain = randomString(15);
    return preDomain ~ "@" ~ dom;
}


string domainFromAddress(in string address)
pure
{
    if (countUntil(address, "@") == -1)
        return "";

    return strip(address).split('@')[1];
}


/**
 * Try to normalize headers to the most common capitalizations
 * RFC 2822 specifies that headers are case insensitive, but better
 * to be safe than sorry
 */
string capitalizeHeader(in string name)
pure
{
    switch(name)
    {
        case "domainkey-signature": return "DomainKey-Signature";
        case "x-spam-setspamtag": return "X-Spam-SetSpamTag";
        default:
    }

    const tokens = split(toLower(name), "-");
    string result;
    foreach(idx, ref tok; tokens)
    {
        if (among(tok, "mime", "dkim", "id", "spf"))
            result ~= toUpper(tok);
        else
            result ~= capitalize(tok);
        if (idx < tokens.length-1)
            result ~= "-";
    }
    return result;
}

string removeStartSlash(in string path)
pure nothrow
{
    return path.startsWith("/") ? path[1..$] : path;
}


string removeEndSlash(string path)
pure nothrow
{
    return path.endsWith("/") ? path[0..$-1] : path;
}


string removeStartEndSlashes(in string path)
pure nothrow
{
    return removeStartSlash(removeEndSlash(path));
}


string ensureStartSlash(in string path)
pure nothrow
{
    return path.startsWith("/") ? path : "/" ~ path;
}


// Quoting utils
enum MAXLINESIZE = 76;
enum HEX = "0123456789ABCDEF";

/**
   Detect = detecting when a char or word should be encoded. All characters
   withing the 0..127 range are safe.
   Header = encode espaces (as underlines), and equal signs
   Body = encode equals, tabs and newlines (but not spaces)
 */
enum QuoteMode
{
    Detect,
    Header,
    Body
}

/**
Decide whether a particular character needs to be quoted.

    The 'quotetabs' flag indicates whether embedded tabs and spaces should be quoted. Note
    that line-ending tabs and spaces are always encoded, as per RFC 1521. The header flag
    indicates if '_' chars must be encoded , as per the almost-Q encoding used to encode MIME
    headers. When 'detecting' '=' chars wont make this return true, since they're valid 7bit
    ASCII (but must be quoted inside a quoted-printable string)
**/
private bool needsQuoting(dchar c, QuoteMode mode)
pure nothrow
{
    if (among(c, '\n', '\r', '\t'))
        return mode == QuoteMode.Body;

    if (among(c, ' ', '_'))
        return mode == QuoteMode.Header;

    if (c == '=')
        return mode != QuoteMode.Detect;

    return !((' ' <= c) && (c <= '~'));
}


bool needsQuoting(string s, QuoteMode mode)
pure
{
    foreach(codepoint; std.range.stride(s, 1))
    {
        if (needsQuoting(codepoint, mode))
            return true;
    }
    return false;
}


private string quote(dchar input, Flag!"Header" headerVariant = No.Header)
pure
{
    string ret = "";
    string utfstr = to!string(input);
    foreach(i; utfstr)
    {
        if (i == ' ' && headerVariant)
            ret ~= '_';
        else
        {
            auto cval = cast(ubyte)i;
            ret ~=['=', HEX[((cval >> 4) & 0x0F)], HEX[(cval & 0x0F)]];
        }
    }
    return ret;
}


string quoteHeader(string word)
pure
{
    // FIXME: this calls needsQuoting twice for every dchar (one time
    // here and one inside the foreach)
    if (!needsQuoting(word, QuoteMode.Detect))
        return word;

    Appender!string res;
    res.put("=?UTF-8?Q?");
    foreach(c; std.range.stride(word, 1))
    {
        if (needsQuoting(c, QuoteMode.Header))
            res.put(quote(c, Yes.Header));
        else
            res.put(c);
    }
    res.put("?=");
    return res.data;
}


string quoteHeaderAddressList(string addresses)
{
    Appender!string resApp;
    auto c = match(addresses, EMAIL_REGEX2);
    bool addSpace = false;
    if (c.pre.length)
    {
        resApp.put(quoteHeader(strip(c.pre)) ~ " ");
    }
    if (c.hit.length)
    {
        resApp.put(c.hit);
    }
    if (c.post.length)
    {
        resApp.put(quoteHeaderAddressList(c.post));
    }
    return resApp.data;
}

string encodeQuotedPrintable(string input, QuoteMode mode, string lineEnd = "\n")
pure
{
    Appender!string retApp;
    string prevLine;
    auto headerVariant = (mode == QuoteMode.Header ? Yes.Header : No.Header);

    void saveLine(string line, string lineEnd = "\n")
    {
        // RFC 1521 requires that the line ending in a space or tab must have that trailing
        // character encoded.
        if (line.length > 1 && among(line[$-1], ' ', '\t'))
            retApp.put(line[0..$-1] ~ quote(line[$-1], headerVariant) ~ lineEnd);
        else
            retApp.put(line ~ lineEnd);
    }

    // we split by lines thus need to know if we've to add a final newline to
    // the last line
    bool addFinalNewline = (input.length && input[$-1] == '\n');

    foreach(line; split(input, lineEnd))
    {
        if (!line.length)
            break;

        Appender!string lineBuffer;
        
        foreach(codepoint; std.range.stride(line, 1))
        {
            if (codepoint == ' ' && mode == QuoteMode.Header)
            {
                lineBuffer.put("_");
            }
            else if (needsQuoting(codepoint, mode))
            {
                lineBuffer.put(quote(codepoint, headerVariant));
            }
            else
            {
                lineBuffer.put(codepoint);
            }
        }

        if (prevLine.length)
            saveLine(prevLine);

        auto thisLine = lineBuffer.data;
        while(thisLine.length > MAXLINESIZE)
        {
            auto limit = MAXLINESIZE-1;
            // dont split by an encoded token
            while ((0 < limit) &&
                  ((1 < limit) && (thisLine[limit-1] == '=')) ||
                  ((2 < limit) && (thisLine[limit-2] == '=')))
            {
                --limit;
            }

            if (!limit) // all "=" chars????
                break;

            saveLine(thisLine[0..limit], "=" ~ lineEnd);
            thisLine = thisLine[limit..$];
        }
        prevLine = thisLine;
    }

    if (prevLine.length)
    {
        string finalLineEnd = addFinalNewline? lineEnd : ""; 
        saveLine(prevLine, finalLineEnd);
    }

    return retApp.data;
}

//  _    _       _ _   _            _
// | |  | |     (_) | | |          | |
// | |  | |_ __  _| |_| |_ ___  ___| |_
// | |  | | '_ \| | __| __/ _ \/ __| __|
// | |__| | | | | | |_| ||  __/\__ \ |_
//  \____/|_| |_|_|\__|\__\___||___/\__|

version(unittest)import std.stdio;

unittest
{
    writeln("Testing Utils.needsQuoting(dchar)");

    dchar c = '\n';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert(!needsQuoting(c, QuoteMode.Header));
    assert(needsQuoting(c, QuoteMode.Body));
    
    c = '\t';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert(!needsQuoting(c, QuoteMode.Header));
    assert( needsQuoting(c, QuoteMode.Body));

    c = '\r';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert(!needsQuoting(c, QuoteMode.Header));
    assert( needsQuoting(c, QuoteMode.Body));

    c = 'a';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert(!needsQuoting(c, QuoteMode.Header));
    assert(!needsQuoting(c, QuoteMode.Body));

    c = 'Ã±';
    assert(needsQuoting(c, QuoteMode.Detect));
    assert(needsQuoting(c, QuoteMode.Header));
    assert(needsQuoting(c, QuoteMode.Body));

    c = ' ';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert( needsQuoting(c, QuoteMode.Header));
    assert(!needsQuoting(c, QuoteMode.Body));

    c = '_';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert( needsQuoting(c, QuoteMode.Header));
    assert(!needsQuoting(c, QuoteMode.Body));

    c = '=';
    assert(!needsQuoting(c, QuoteMode.Detect));
    assert( needsQuoting(c, QuoteMode.Header));
    assert( needsQuoting(c, QuoteMode.Body));
}

unittest
{
    writeln("Testing Utils.needsQuoting(string)");
    string s = "AANLkTi=KRf9FL0EqQ0AVm=pA3DCBgiXYR=vnECs1gUMe@mail.gmail.com";
    assert(quoteHeader(s) == s);

    s = "Juanjo Ãlvarez <juanjux@gmail.com>";
    auto q = quoteHeader(s);
    assert(q == "=?UTF-8?Q?Juanjo_=C3=81lvarez_<juanjux@gmail.com>?=");
}

unittest
{
    writeln("Testing Utils.quote");

    dchar c = 'a';
    assert(quote(c) == "=61");
    c = ' ';
    assert(quote(c) == "=20");
    c = 'Ã±';
    assert(quote(c) == "=C3=B1");
}

unittest
{
    writeln("Testing Utils.quoteHeaderAddressList");

    string addrList = "Juanjo Ãlvarez <juanjux@gmail.com>, "~
                      "Pepito Perez <pepe@perez.com>, "~
                      "Manolo ÃoÃ±o manolo@gmail.com";
    auto res = quoteHeaderAddressList(addrList);
    assert(res == "=?UTF-8?Q?Juanjo_=C3=81lvarez?= <juanjux@gmail.com>, Pepito Perez <pepe@perez.com>=?UTF-8?Q?,_Manolo_=C3=91o=C3=B1o?= manolo@gmail.com");
}

unittest
{
    writeln("Testing Utils.encodeQuotedPrintable");

    string a = "abc\ndeÃ±Ã±Ã¡lolo\n";
    auto res1 = encodeQuotedPrintable(a, QuoteMode.Header);
    assert(res1 == "abc\nde=C3=B1=C3=B1=C3=A1lolo\n");
    assert(a == decodeQuotedPrintable(res1, true));

    string b = "abc\nsometab\tandsomemore\t\n";
    auto res2 = encodeQuotedPrintable(b, QuoteMode.Header);
    // Tabs before newline should be encoded always
    assert(res2 == "abc\nsometab\tandsomemore=09\n");
    assert(b == decodeQuotedPrintable(res2, true));
    assert(encodeQuotedPrintable(b, QuoteMode.Detect) ==
           "abc\nsometab\tandsomemore=09\n");
    auto res2_1 = encodeQuotedPrintable(b, QuoteMode.Body);
    assert(res2_1 == "abc\nsometab=09andsomemore=09\n");

    string c = "abc\nwith spaces end\n";
    assert(encodeQuotedPrintable(c, QuoteMode.Header) == "abc\nwith_spaces_end\n");
    assert(encodeQuotedPrintable(c, QuoteMode.Detect) == "abc\nwith spaces end\n");

    string d = "Ã±aÃ±a\npo Ã±aÃ±a\tla";
    auto res3 = encodeQuotedPrintable(d, QuoteMode.Body);
    assert(res3 == "=C3=B1a=C3=B1a\npo =C3=B1a=C3=B1a=09la");
    assert(d == decodeQuotedPrintable(res3, false));

    string f = "one Ã±aÃ±a two Ã±oÃ±o three Ã¡Ã¡Ã¡ four Ã©Ã©Ã© five" ~
                  " Ã±aÃ±a six Ã±aÃ±a seven Ã±aÃ±a eight Ã±aÃ±a\n";
    auto res4 = encodeQuotedPrintable(f, QuoteMode.Detect);
    assert(f == decodeQuotedPrintable(res4, false));
    res4 = encodeQuotedPrintable(f, QuoteMode.Header);
    assert(f == decodeQuotedPrintable(res4, true));
    res4 = encodeQuotedPrintable(f, QuoteMode.Body);
    assert(f == decodeQuotedPrintable(res4, false));
    
    string quijotipsum = "En un lugar de la Mancha, de cuyo nombre no quiero "~
        "acordarme, no ha mucho tiempo que vivÃ­a un hidalgo de los de lanza en "~
        "astillero, adarga antigua, rocÃ­n flaco y galgo corredor. Una olla de "~
        "algo mÃ¡s vaca que carnero, salpicÃ³n las mÃ¡s noches, duelos y quebrantos"~
        " los sÃ¡bados, lantejas los viernes, algÃºn palomino de aÃ±adidura los"~
        " domingos, consumÃ­an las tres partes de su hacienda. El resto della"~
        " concluÃ­an sayo de velarte, calzas de velludo para las fiestas, con"~
        " sus pantuflos de lo mesmo, y los dÃ­as de entresemana se honraba con"~
        " su vellorÃ­ de lo mÃ¡s fino. TenÃ­a en su casa una ama que pasaba de los"~
        " cuarenta, y una sobrina que no llegaba a los veinte, y un mozo de"~
        " campo y plaza, que asÃ­ ensillaba el rocÃ­n como tomaba la podadera."~
        " Frisaba la edad de nuestro hidalgo con los cincuenta aÃ±os; era de"~
        " complexiÃ³n recia, seco de carnes, enjuto de rostro, gran madrugador"~
        " y amigo de la caza. Quieren decir que tenÃ­a el sobrenombre de"~
        " Quijada, o Quesada, que en esto hay alguna diferencia en los autores"~
        " que deste caso escriben; aunque, por conjeturas verosÃ­miles, se deja"~
        " entender que se llamaba Quejana. Pero esto importa poco a nuestro"~
        " cuento; basta que en la narraciÃ³n dÃ©l no se salga un punto de la verdad.\n"; 
    auto res5 = encodeQuotedPrintable(quijotipsum, QuoteMode.Detect);
    assert(quijotipsum == decodeQuotedPrintable(res5, false));
    res5 = encodeQuotedPrintable(quijotipsum, QuoteMode.Header);
    assert(quijotipsum == decodeQuotedPrintable(res5, true));
    res5 = encodeQuotedPrintable(quijotipsum, QuoteMode.Body);
    assert(quijotipsum == decodeQuotedPrintable(res5, false));

    string g = "Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡Ã¡";
    auto res6 = encodeQuotedPrintable(g, QuoteMode.Body);
    auto shouldBeRes6 =
        "=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=\n"~
        "=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=\n"~
        "=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=\n"~
        "=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=\n"~
        "=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=A1=C3=\n"~
        "=A1=C3=A1=C3=A1=C3=A1";
    assert(res6 == shouldBeRes6);
    assert(g == decodeQuotedPrintable(res6));
    res6 = encodeQuotedPrintable(g, QuoteMode.Detect);
    assert(g == decodeQuotedPrintable(res6));
    res6 = encodeQuotedPrintable(g, QuoteMode.Header);
    assert(g == decodeQuotedPrintable(res6, true));

    // test that it doesn't strips the last \n
    string h = g ~ "\n";
    auto res7 = encodeQuotedPrintable(h, QuoteMode.Header);
    auto shouldBeRes7 = shouldBeRes6 ~ "\n";
    assert(shouldBeRes7 == res7);
    assert(h == decodeQuotedPrintable(res7));
}

unittest
{
    writeln("Testing Utils.removeDups");
    assert(removeDups(["a", "b", "cde", "fg"]) == ["a", "b", "cde", "fg"]);
    assert(removeDups(["a", "a", "b", "cde", "cde", "fg"]) == ["a", "b", "cde", "fg"]);
    assert(removeDups(["a", "b", "cde", "a", "fg", "b"]) == ["a", "b", "cde", "fg"]);
    int[] empty;
    assert(removeDups(empty).length == 0);
    assert(removeDups([3, 5, 16, 23, 5]) == [3, 5, 16, 23]);
    assert(removeDups([3,3,3,3,3,3,3,3,3,3,3]) == [3]);
}

unittest
{
    writeln("Testing Utils.randomString");
    assert(randomString(10).length == 10);
    assert(randomString(1000).length == 1000);
}

unittest
{
    writeln("Testing Utils.randomFileName");
    auto rf = randomFileName(buildPath("home", "test"), ".jpg");
    assert(rf.startsWith(buildPath("home", "test")));
    assert(rf.endsWith(".jpg"));
    rf = randomFileName("", ".jpg");
    assert(rf.countUntil("/") == -1);
    assert(rf.endsWith(".jpg"));
    rf = randomFileName("", "jpg");
    assert(rf.endsWith(".jpg"));
    rf = randomFileName("", "");
    assert(rf.countUntil(".") == -1);
}

unittest
{
    writeln("Testing Utils.generateMessageId");
    auto domain = "somedomain.com";
    auto msgid = generateMessageId(domain);
    assert(msgid.countUntil(domain) != -1);
    assert(msgid.endsWith("@" ~ domain));
    msgid = generateMessageId();
    assert(msgid.countUntil("@") != -1);
    assert(!msgid.startsWith("@"));
    assert(msgid.endsWith(".com"));
}


unittest
{
    writeln("Testing Utils.domainFromAddress");
    assert(domainFromAddress("someaddr@somedomain.com") == "somedomain.com");
    assert(domainFromAddress("someaddr@") == "");
    assert(domainFromAddress("someaddr") == "");
    assert(domainFromAddress("") == "");
    assert(domainFromAddress("someaddr@somedomain@someother.com") == "somedomain");
}

unittest
{
    writeln("Testing Utils.capitalizeHeader");
    assert(capitalizeHeader("mime-version")   == "MIME-Version");
    assert(capitalizeHeader("subject")        == "Subject");
    assert(capitalizeHeader("received-spf")   == "Received-SPF");
    assert(capitalizeHeader("dkim-signature") == "DKIM-Signature");
    assert(capitalizeHeader("message-id")     == "Message-ID");
}

unittest
{
    writeln("Testing Utils.lowStartsWith");
    assert(lowStartsWith("abcDEF", "abcd"));
    assert(lowStartsWith("hijk", ""));
}


unittest
{
    writeln("Testing Utils.lowStrip");
    assert(lowStrip(" \tabcDEF") == "abcdef");
    assert(lowStrip(" ABCdef \t \n") == "abcdef");
}


unittest
{
    writeln("Testing Utils.removeStartSlash/End/StartEnd");
    assert(removeStartSlash("abcDEF") == "abcDEF");
    assert(removeStartSlash("/abcdef") == "abcdef");
    assert(removeStartSlash("/abcdef/") == "abcdef/");
    assert(removeStartSlash("///abcdef") == "//abcdef");

    assert(removeEndSlash("abcDEF") == "abcDEF");
    assert(removeEndSlash("abcDEF/") == "abcDEF");
    assert(removeEndSlash("abcDEF///") == "abcDEF//");
    assert(removeEndSlash("/abcDEF/") == "/abcDEF");

    assert(removeStartEndSlashes("abcDEF") == "abcDEF");
    assert(removeStartEndSlashes("/abcDEF") == "abcDEF");
    assert(removeStartEndSlashes("//abcDEF//") == "/abcDEF/");
    assert(removeStartEndSlashes("abcDEF//") == "abcDEF/");

}

unittest // clearSubject
{
    writeln("Testing Utils.clearSubject");
    assert(clearSubject("RE: polompos") == "polompos");
    assert(clearSubject("Re: cosa RE: otracosa re: mascosas") == "cosa otracosa mascosas");
    assert(clearSubject("Pok and something Re: things") == "Pok and something things");
}
